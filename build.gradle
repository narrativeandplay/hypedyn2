plugins {
    id "com.github.hierynomus.license" version "0.11.0"
    id "com.github.ben-manes.versions" version "0.12.0"
}

apply from: 'javafx.plugin'

dependencies {
    compile project(':hypedyn-ui')
}

javafx {
    appName 'HypeDyn 2'
    mainClass 'org.narrativeandplay.hypedyn.Main'
}

version = "0.22.2-alpha"

ext {
    majorScalaVersion = "2.11"
    minorScalaVersion = "7"
}

allprojects {
    apply plugin: 'scala'

    repositories {
        jcenter()
        mavenCentral()

        flatDir {
            dirs "${rootDir.path}/libs"
        }
    }

    sourceCompatibility = 1.8

    dependencies {
        compile "org.scala-lang:scala-library:${rootProject.majorScalaVersion}.${rootProject.minorScalaVersion}"

        testCompile 'junit:junit:4.12'
        testCompile "org.scalatest:scalatest_${rootProject.majorScalaVersion}:2.2.4"
        testRuntime "org.scala-lang.modules:scala-xml_${rootProject.majorScalaVersion}:1.0.5"
    }

    tasks.withType(ScalaCompile) {
        scalaCompileOptions.with {
            additionalParameters = [
                    "-Xexperimental", // for Single Abstract Method conversion support without needing implicits
                    "-Xlint:-nullary-unit,_",
                    "-feature",
                    "-Xfatal-warnings",
                    "-deprecation",
                    "-unchecked",
                    "-Yinline-warnings"
            ]
        }
    }
}
